
//Curvature energy definition
quantity starsq energy method star_sq_mean_curvature global

//some definitions to make things below easier
#define phi (pi/10)
#define theta (pi/4)

#define prodist (2) //distance of center of protein from edge of semicircle
#define prorad  (1) //radius of the circle that will represent the protein
#define proang  (15*pi/180) //angle between z-axis and "polar" axis of protein
#define contang (90*pi/180) //angle of contact between protein and membrane

//defining values for the "sphere" that a portion of will be used to represent the protein
#define sphrad  (prorad/sin(contang)) //radius of sphere
#define sphcen2procen (sqrt(sphrad^2-prorad^2)) //distance between center of sphere and center of "protein cap"
#define sphhght (-cos(proang+contang)*sphrad) //height of sphere center from x-y-plane
#define sphdist (prodist-sphcen2procen*sin(proang)) //distance of sphere's center from x-z plane
#define prohght (prorad*sin(proang)) //height of protein center
#define prothk  (sphrad-sphcen2procen) //thickness of protein (distance from center on plane of contact circle to top of protein cap)


//defining values/equations for the plane that holds the contact circle between protein and membrane
//yplane*(y-prodist) + zplane*(z-prohght) = 0
#define yplane (sin(proang))
#define zplane (cos(proang))

//start making values for circle of protein contacting the membrane
#define prox1 (prorad)
#define prox2 (prorad/2)
#define prox3 (-prox2)
#define prox4 (-prorad)
#define prox5 (-prox2)
#define prox6 (prox2)

#define y2sqandz2sq (prorad^2-prox2^2) //y^2 + z^2 in the sphere's equation for 2nd point

//IF proang is 90, comment out this next line and uncomment y2sq=0 line
#define y2sq (y2sqandz2sq/(1+yplane^2/zplane^2)) //y^2 in sphere's equation for 2nd point
//#define y2sq (0)

//IF proang is 0, comment out this next line and uncomment z2sq=0 line
#define z2sq (y2sqandz2sq/(1+zplane^2/yplane^2)) //z^2 in sphere's equation for 2nd point
//#define z2sq (0)


#define proy1 (prodist)
#define proy2 (sqrt(y2sq)+prodist)
#define proy3 (proy2)
#define proy4 (prodist)
#define proy5 (-sqrt(y2sq)+prodist)
#define proy6 (proy5)

#define proz1 (prohght)
#define proz2 (-sqrt(z2sq)+prohght)
#define proz3 (proz2)
#define proz4 (prohght)
#define proz5 (sqrt(z2sq)+prohght)
#define proz6 (proz5)

//Some definitions for intermediary circle
#define protoedge (proy5) //distance from near edge of protein to x-z-plane
#define cirrad (prorad+protoedge/2) //radius of circle intermediary

#define cirx1 (cirrad)
#define cirx2 (cirx1/2)
#define cirx3 (-cirx2)
#define cirx4 (-cirx1)
#define cirx5 (-cirx2)
#define cirx6 (cirx2)

#define ciry1 ((proy2+zplane*protoedge/2+protoedge/2)/2)
#define ciry2 (proy2+zplane*protoedge/2)
#define ciry3 (ciry2)
#define ciry4 (ciry1)
#define ciry5 (proy5/2)
#define ciry6 (protoedge/2)

#define cirz1 ((proz2-yplane*protoedge/2+proz5)/2)
#define cirz2 (proz2-yplane*protoedge/2)
#define cirz3 (cirz2)
#define cirz4 (cirz1)
#define cirz5 (proz5)
#define cirz6 (cirz5)

//Some definitions for circle that touches x-z-plane

#define pcirx1 (prorad+protoedge)
#define pcirx2 (pcirx1/2)
#define pcirx3 (-pcirx2)
#define pcirx4 (-pcirx1)
#define pcirx5 (-pcirx2)
#define pcirx6 (pcirx2)

#define pciry1 ((proy2+zplane*protoedge)/2)
#define pciry2 (proy2+zplane*protoedge)
#define pciry3 (pciry2)
#define pciry4 (pciry1)
#define pciry5 (0)
#define pciry6 (pciry5)

#define pcirz1 ((proz2-yplane*protoedge+cirz5)/2)
#define pcirz2 (proz2-yplane*protoedge)
#define pcirz3 (pcirz2)
#define pcirz4 (pcirz1)
#define pcirz5 (cirz5)
#define pcirz6 (pcirz5)



//Constraints for Protein

constraint 1 //"sphere" for which the cap represents the protein
formula: x^2 + (y-sphdist)^2 + (z-sphhght)^2 = (sphrad)^2

constraint 2 //plane that the base of protein is constrainted to
formula: yplane*(y-prodist) + zplane*(z-prohght) = 0

parameter flutey_count=3
constraint 3 //invisible flutes that prevent circle vertices of protein from "short-circuiting"
formula: sin(flutey_count*atan2(zplane*(y-prodist)-yplane*(z-prohght),x))


//any other constraints
//constraints for outer massive membrane
constraint 5
formula: x^2 + y^2 = 104857600

parameter flute_count = 10
constraint 6
formula: sin(flute_count*atan2(y,x))

constraint 7
formula: z = 0

constraint 8
formula: y = 0



Vertices

//Protein base (0)
1 prox1 proy1 proz1 constraint 1,2,3
2 prox2 proy2 proz2 constraint 1,2,3
3 prox3 proy3 proz3 constraint 1,2,3
4 prox4 proy4 proz4 constraint 1,2,3
5 prox5 proy5 proz5 constraint 1,2,3
6 prox6 proy6 proz6 constraint 1,2,3
7 0  prodist+prothk*yplane  prohght+prothk*zplane  constraint 1

//first intermediary, immediately outside the protein circle (1)
18 cirx1 ciry1 cirz1
19 cirx2 ciry2 cirz2
20 cirx3 ciry3 cirz3
21 cirx4 ciry4 cirz4
22 cirx5 ciry5 cirz5
23 cirx6 ciry6 cirz6

//first merger onto y = 0 plane (2)
24 pcirx1 pciry1 pcirz1
25 pcirx2 pciry2 pcirz2
26 pcirx3 pciry3 pcirz3
27 pcirx4 pciry4 pcirz4
28 pcirx5 pciry5 pcirz5 constraint 8
29 pcirx6 pciry6 pcirz6 constraint 8



Edges
/*key:
S(single layer)/D(double, layer-to-layer connection)
NUMBER(1 is protein circle, 100 is edge of membrane,
- to signify from one layer to the next ie 2-3)
*/

//inner protein circle (ES0)
1   1  2 constraint 1,2,3
2   2  3 constraint 1,2,3
3   3  4 constraint 1,2,3
4   4  5 constraint 1,2,3
5   5  6 constraint 1,2,3
6   6  1 constraint 1,2,3

//making the protein "cap"
7  1 7 constraint 1
8  2 7 constraint 1
9  3 7 constraint 1
10 4 7 constraint 1
11 5 7 constraint 1
12 6 7 constraint 1

//first intermediary to connect protein circle to (ES1)
28 18 19
29 19 20
30 20 21
31 21 22
32 22 23
33 23 18

//first merger to plane connections and on plane parts (ES2)
34 24 25
35 25 26
36 26 27
37 27 28
38 28 29 constraint 8
39 29 24


//layer to layer connections:
//Protein to first circle intermediary (ED0-1)
101  1 18
102  2 19
103  3 20
104  4 21
105  5 22
106  6 23

//Circle intermediary to next intermediary (ED1-2)
107 18 24
108 19 25
109 20 26
110 21 27
111 22 28
112 23 29



Faces
/*key:
NUMBER(1 is protein circle, 100 is edge of membrane,
- to signify from one layer to the next ie 2-3)
*/

//Spherical cap (F0)
1 1  8 -7  constraint 1 TENSION 0
2 2  9 -8  constraint 1 TENSION 0
3 3 10 -9  constraint 1 TENSION 0
4 4 11 -10 constraint 1 TENSION 0
5 5 12 -11 constraint 1 TENSION 0
6 6  7 -12 constraint 1 TENSION 0

//Two Circles (F0-1)
13 28 -102 -1 101 TENSION 0
14 29 -103 -2 102 TENSION 0
15 30 -104 -3 103 TENSION 0
16 31 -105 -4 104 TENSION 0
17 32 -106 -5 105 TENSION 0
18 33 -101 -6 106 TENSION 0

//Circle to edge circle constraint intermediary (F1-2)
19 34 -108 -28 107 TENSION 0
20 35 -109 -29 108 TENSION 0
21 36 -110 -30 109 TENSION 0
22 37 -111 -31 110 TENSION 0
23 38 -112 -32 111 TENSION 0
24 39 -107 -33 112 TENSION 0



read

r :::= { flute_count *= 2; flutey_count *= 2; 'r' }
//refine edge :::= { flute_count *= 2; flutey_count *= 2; 'refine edge'}



