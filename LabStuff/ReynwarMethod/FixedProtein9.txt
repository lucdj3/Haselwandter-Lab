//some definitions to make things below easier
#define phi (pi/12)
#define theta (pi/4)

quantity bendergy energy method star_sq_mean_curvature global

//constraints for protein cylinder
constraint 1
formula: (x)^2 + (y-15)^2 = 100

constraint 2
formula: z = 2

parameter flutey_count = 12
constraint 3
formula: sin(flutey_count*atan2(y-15,x))


//constraints for outer massive membrane
constraint 5
formula: x^2 + y^2 = 104857600

parameter flute_count = 12
constraint 6
formula: sin(flute_count*atan2(y,x))

constraint 7
formula: z = 0

constraint 8
formula: y = 0

vertices

//inner protein vertices
1 10*cos(0*phi)  10*sin(0*phi)+15  2 constraint 1,2,3
2 10*cos(4*phi)  10*sin(4*phi)+15  2 constraint 1,2,3
3 10*cos(8*phi)  10*sin(8*phi)+15  2 constraint 1,2,3
4 10*cos(12*phi) 10*sin(12*phi)+15 2 constraint 1,2,3
5 10*cos(16*phi) 10*sin(16*phi)+15 2 constraint 1,2,3
6 10*cos(20*phi) 10*sin(20*phi)+15 2 constraint 1,2,3
//more to make the sphere
7  10*sin(2*theta)*cos(0*phi)  10*sin(2*theta)*sin(0*phi)+15  10*cos(1*theta)+2 constraint 1,3 fixed
8  10*sin(2*theta)*cos(4*phi)  10*sin(2*theta)*sin(4*phi)+15  10*cos(1*theta)+2 constraint 1,3 fixed
9  10*sin(2*theta)*cos(8*phi)  10*sin(2*theta)*sin(8*phi)+15  10*cos(1*theta)+2 constraint 1,3 fixed
10 10*sin(2*theta)*cos(12*phi) 10*sin(2*theta)*sin(12*phi)+15 10*cos(1*theta)+2 constraint 1,3 fixed
11 10*sin(2*theta)*cos(16*phi) 10*sin(2*theta)*sin(16*phi)+15 10*cos(1*theta)+2 constraint 1,3 fixed
12 10*sin(2*theta)*cos(20*phi) 10*sin(2*theta)*sin(20*phi)+15 10*cos(1*theta)+2 constraint 1,3 fixed


//outer edge of membrane vertices
14 10240*cos(0*phi)  10240*sin(0*phi)  0 constraint 5,7,8
15 10240*cos(4*phi)  10240*sin(4*phi)  0 constraint 5,6,7
16 10240*cos(8*phi)  10240*sin(8*phi)  0 constraint 5,6,7
17 10240*cos(12*phi) 10240*sin(12*phi) 0 constraint 5,7,8


//first intermediary, immediately outside the protein circle
18 12*cos(0*phi)  12*sin(0*phi)+15  1
19 12*cos(4*phi)  12*sin(4*phi)+15  1
20 12*cos(8*phi)  12*sin(8*phi)+15  1
21 12*cos(12*phi) 12*sin(12*phi)+15 1
22 12*cos(16*phi) 12*sin(16*phi)+15 1
23 12*cos(20*phi) 12*sin(20*phi)+15 1


//first merger onto y = 0 plane
24 40*cos(0*phi)  40*sin(0*phi)  0 constraint 8
25 40*cos(4*phi)  40*sin(4*phi)  0
26 40*cos(8*phi)  40*sin(8*phi)  0
27 40*cos(12*phi) 40*sin(12*phi) 0 constraint 8
28 -15            0              0 constraint 8
29 15             0              0 constraint 8


//another intermediary merged to y = 0 plane (160 radius)
30 160*cos(0*phi)  160*sin(0*phi)  0 constraint 8
31 160*cos(4*phi)  160*sin(4*phi)  0
32 160*cos(8*phi)  160*sin(8*phi)  0
33 160*cos(12*phi) 160*sin(12*phi) 0 constraint 8


edges

/*
key:
S(single layer)/D(double, layer-to-layer connection)
NUMBER(1 is protein circle, 100 is edge of membrane,
- to signify from one layer to the next ie 2-3)
*/

//inner protein circle (ES1)
1   1  2 constraint 1,2,3
2   2  3 constraint 1,2,3
3   3  4 constraint 1,2,3
4   4  5 constraint 1,2,3
5   5  6 constraint 1,2,3
6   6  1 constraint 1,2,3
//making the spherical cap
7   7  8 constraint 1 fixed no_refine
8   8  9 constraint 1 fixed no_refine
9   9 10 constraint 1 fixed no_refine
10 10 11 constraint 1 fixed no_refine
11 11 12 constraint 1 fixed no_refine
12 12  7 constraint 1 fixed no_refine
13  1  7 constraint 1 fixed no_refine
14  2  8 constraint 1 fixed no_refine
15  3  9 constraint 1 fixed no_refine
16  4 10 constraint 1 fixed no_refine
17  5 11 constraint 1 fixed no_refine
18  6 12 constraint 1 fixed no_refine


//outer membrane connections (ES100)
25 14 15 constraint 5,6,7
26 15 16 constraint 5,6,7
27 16 17 constraint 5,6,7

//first intermediary to connect protein circle to (ES2)
28 18 19
29 19 20
30 20 21
31 21 22
32 22 23
33 23 18

//first merger to plane connections and on plane parts (ES3)
34 24 25
35 25 26
36 26 27
37 27 28 constraint 8
38 28 29 constraint 8
39 29 24 constraint 8

//next intermediary (ES4)
40 30 31
41 31 32
42 32 33


//layer to layer connections:
//Protein to first circle intermediary (ED1-2)
101  1 18
102  2 19
103  3 20
104  4 21
105  5 22
106  6 23

//Circle intermediary to next intermediary (ED2-3)
107 18 24
108 19 25
109 20 26
110 21 27
111 22 28
112 23 29

//3rd level to 4th level (ED3-4)
113 24 30 constraint 8
114 25 31
115 26 32
116 27 33 constraint 8

//4th level to outer membrane (ED4-100)
117 30 14 constraint 8
118 31 15
119 32 16
120 33 17 constraint 8

faces
/*
key:
NUMBER(1 is protein circle, 100 is edge of membrane,
- to signify from one layer to the next ie 2-3)
*/

//Spherical cap (F1)
1   1 14  -7 -13 constraint 1 TENSION 0 fixed no_refine
2   2 15  -8 -14 constraint 1 TENSION 0 fixed no_refine
3   3 16  -9 -15 constraint 1 TENSION 0 fixed no_refine
4   4 17 -10 -16 constraint 1 TENSION 0 fixed no_refine
5   5 18 -11 -17 constraint 1 TENSION 0 fixed no_refine
6   6 13 -12 -18 constraint 1 TENSION 0 fixed no_refine

//Two Circles (F1-2)
13 28 -102 -1 101 TENSION 0
14 29 -103 -2 102 TENSION 0
15 30 -104 -3 103 TENSION 0
16 31 -105 -4 104 TENSION 0
17 32 -106 -5 105 TENSION 0
18 33 -101 -6 106 TENSION 0

//Outer Circle to edge constraint intermediary (F2-3)
19 34 -108 -28 107 TENSION 0
20 35 -109 -29 108 TENSION 0
21 36 -110 -30 109 TENSION 0
22 37 -111 -31 110 TENSION 0
23 38 -112 -32 111 TENSION 0
24 39 -107 -33 112 TENSION 0

//Intermediary (3) to intermediary (4) (F3-4)
25 40 -114 -34 113 TENSION 0
26 41 -115 -35 114 TENSION 0
27 42 -116 -36 115 TENSION 0

//Intermediary (4) to outer membrane (F4-100)
28 25 -118 -40 117 TENSION 0
29 26 -119 -41 118 TENSION 0
30 27 -120 -42 119 TENSION 0

read

r :::= { flute_count *= 2; flutey_count *= 2; 'r' }
//refine edge :::= { flute_count *= 2; flutey_count *= 2; 'refine edge'}
